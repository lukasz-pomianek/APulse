buildscript {
    ext.kotlin_version = "1.9.20"
    ext.agp_version = "8.2.0"
    
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
    
    dependencies {
        classpath "com.android.tools.build:gradle:$agp_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "com.google.dagger:hilt-android-gradle-plugin:2.48.1"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
    // Align group & version for JitPack. VERSION env is set by JitPack to the tag (e.g., v1.0.18)
    group = 'com.github.lukasz-pomianek'
    version = System.getenv("VERSION") ?: "LOCAL"
}

subprojects {
    afterEvaluate {
        if (hasProperty('android')) {
            android {
                compileOptions {
                    sourceCompatibility = JavaVersion.VERSION_17
                    targetCompatibility = JavaVersion.VERSION_17
                }
            }
        }
    }
    
    tasks.withType(JavaCompile).configureEach {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }
    
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}